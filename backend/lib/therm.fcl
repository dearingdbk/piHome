FUNCTION_BLOCK Fuzzy_FB
    VAR_INPUT
        temp : REAL;
        time : REAL;
  outside: REAL;
    END_VAR

    VAR_OUTPUT
        heater : REAL;
    END_VAR

    FUZZIFY temp
        TERM hot :=  (20, 0) (24, 1);
	TERM cool := (16, 0) (18, 1) (20, 0);
	TERM cold := (12, 1) (16, 0);
    END_FUZZIFY

    FUZZIFY time
        TERM home  := (0, 1) (9, 0) (15, 0) (18, 1) (24, 1);
	TERM asleep := (0, 1) (6, 0) (20, 0) (24, 1);
    END_FUZZIFY

    FUZZIFY outside
    	TERM summer := (10, 0)(20, 1);
	TERM winter := (0, 1)(10, 0);
    END_FUZZIFY

    DEFUZZIFY heater
        TERM on := 1;
        TERM off := 0;
        ACCU : MAX;
        METHOD : COGS;
        DEFAULT := 0;
    END_DEFUZZIFY
    RULEBLOCK No1
        AND : MIN;
	RULE 1 : IF temp IS cold AND outside IS winter THEN heater IS on;
	RULE 2 : IF temp IS cool AND outside IS summer THEN heater IS off;
        RULE 3 : IF temp IS cool AND time IS asleep THEN heater IS off;
	RULE 4 : IF temp IS cool AND time IS NOT asleep AND time IS home THEN heater IS on;
	RULE 5 : IF temp IS hot THEN heater IS off;
    END_RULEBLOCK
END_FUNCTION_BLOCK
